## Phylogenetics project
# Coral diversification rates vs environmental variables

# set working directory
setwd("C:/Users/lars-/Desktop/Uni/Master/2. Semester/Phylogenetics/Phylogenetics project")

# Read in the observed tree
T <- readTrees("data/Scleractinia_species_level.tre") [1]

taxa <- T.taxa() # taxa information for birth-death process

moves    = VectorMoves()
monitors = VectorMonitors()

# Set up environmental data
# var <- v(297.6, 301.36, 304.84, 307.86, 310.36, 312.53, 314.48, 316.31, 317.42, 317.63, 317.74, 318.51, 318.29, 316.5, 315.49, 317.64, 318.61, 316.6, 317.77, 328.27, 351.12, 381.87, 415.47, 446.86, 478.31, 513.77, 550.74, 586.68, 631.48, 684.13, 725.83, 757.81, 789.39, 813.79, 824.25, 812.6, 784.79, 755.25, 738.41, 727.53, 710.48, 693.55, 683.04, 683.99, 690.93, 694.44, 701.62, 718.05, 731.95, 731.56, 717.76)
var <- v(0.7090184, 0.7087190, 0.7081701, 0.7078171, 0.7077420, 0.7077501, 0.7078148, 0.7076480, 0.7074318, 0.7073783, 0.7074178,
        0.7072761, 0.7073982, 0.7073793, 0.7071852, 0.7069474, 0.7069454, 0.7072605, 0.7071754, 0.7075384, 0.7077311, 0.7078536,
        0.7077222, 0.7076284, 0.7078606)

MAX_VAR_AGE = 250 # maximum age of measurements

NUM_INTERVALS = var.size()-1 # helper variable for number of intervals

# Setting up the time intervals
interval_times <- MAX_VAR_AGE * (1:NUM_INTERVALS) / NUM_INTERVALS

## Specifying the model
# Priors
SD = abs(0.587405 / NUM_INTERVALS) # exponential hyperprior to estimate sd

speciation_sd ~ dnExponential( 1.0 / SD)
extinction_sd ~ dnExponential( 1.0 / SD)

moves.append( mvScale(speciation_sd, weight=5.0) )
moves.append( mvScale(extinction_sd, weight=5.0) )

# Specifying the correlation coefficients
beta_speciation ~ dnNormal(0,1.0)
beta_extinction ~ dnNormal(0,1.0)

moves.append( mvSlide(beta_speciation,delta=1.0,weight=10.0) )
moves.append( mvSlide(beta_extinction,delta=1.0,weight=10.0) )

speciation_corr_neg_prob := ifelse(beta_speciation < 0.0, 1, 0)
extinction_corr_neg_prob := ifelse(beta_extinction < 0.0, 1, 0)
speciation_corr_pos_prob := ifelse(beta_speciation > 0.0, 1, 0)
extinction_corr_pos_prob := ifelse(beta_extinction > 0.0, 1, 0)

# Specifying correlated rates
log_speciation[1] ~ dnUniform(-10.0,10.0)
log_speciation[1].setValue(0.0)
log_extinction[1] ~ dnUniform(-10.0,10.0)
log_extinction[1].setValue(-1.0)

moves.append( mvSlide(log_speciation[1], weight=2) )
moves.append( mvSlide(log_extinction[1], weight=2) )

speciation[1] := exp( log_speciation[1] )
extinction[1] := exp( log_extinction[1] )

# speciation and extinction rates for each time interval
for (i in 1:NUM_INTERVALS) {
    index = i+1

    expected_speciation[i] := log_speciation[i] + beta_speciation * ln( var[index] / var[i] )
    expected_extinction[i] := log_extinction[i] + beta_extinction * ln( var[index] / var[i] )

    log_speciation[index] ~ dnNormal( mean=expected_speciation[i], sd=speciation_sd )
    log_extinction[index] ~ dnNormal( mean=expected_extinction[i], sd=extinction_sd )

    moves.append( mvSlide(log_speciation[index], weight=2) )
    moves.append( mvSlide(log_extinction[index], weight=2) )

    speciation[index] := exp( log_speciation[index] )
    extinction[index] := exp( log_extinction[index] )

}

moves.append( mvVectorSlide(log_speciation, weight=10) )
moves.append( mvVectorSlide(log_extinction, weight=10) )

moves.append( mvShrinkExpand(log_speciation, sd=speciation_sd, weight=10) )
moves.append( mvShrinkExpand(log_extinction, sd=extinction_sd, weight=10) )

# Incomplete taxa sampling
rho <- T.ntips()/780

# Root age
root_time <- T.rootAge()

# Time tree for full episodic birth-death model
timetree ~ dnEpisodicBirthDeath(rootAge=T.rootAge(), lambdaRates=speciation, lambdaTimes=interval_times, muRates=extinction, muTimes=interval_times, rho=rho, samplingStrategy="uniform", condition="survival", taxa=taxa)

timetree.clamp(T)
mymodel = model(speciation)

# Running MCMC analysis
monitors.append( mnModel(filename="output/Sr_corals.log",printgen=10, separator = TAB) )

monitors.append( mnFile(filename="output/Sr_corals_EBD_Corr_speciation_rates.log",printgen=10, separator = TAB, speciation) )
monitors.append( mnFile(filename="output/Sr_corals_EBD_Corr_speciation_times.log",printgen=10, separator = TAB, interval_times) )
monitors.append( mnFile(filename="output/Sr_corals_EBD_Corr_extinction_rates.log",printgen=10, separator = TAB, extinction) )
monitors.append( mnFile(filename="output/Sr_corals_EBD_Corr_extinction_times.log",printgen=10, separator = TAB, interval_times) )

monitors.append( mnScreen(printgen=1000, beta_speciation, beta_extinction) )

# Initializing and Running the MCMC Simulation
mymcmc = mcmc(mymodel, monitors, moves, nruns=1, combine="mixed")

# Run MCMC
mymcmc.run(generations=500000, tuningInterval=200)
